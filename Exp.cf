entrypoints Module, Exp ;

comment "--" ;
comment "{-" "-}" ;

layout "where", "let", "mutual", "split" ;
layout stop "in" ;
-- Do not use layout toplevel as it makes pExp fail!

Module.   Module ::= "module" AIdent "where" "{" [Imp] [Decl] "}" ;

Import.   Imp ::= "import" AIdent ;
separator Imp ";" ;

DeclDef.    Decl ::= AIdent [Tele] ":" Exp "=" ExpWhere ;
DeclData.   Decl ::= "data" AIdent [Tele] "=" [Label] ;
DeclSplit.  Decl ::= AIdent [Tele] ":" Exp "=" "split" "{" [Branch] "}" ;
-- DeclMutual. Decl ::= "mutual" "{" [Decl] "}" ;
separator   Decl ";" ;

Where.    ExpWhere ::= Exp "where" "{" [Decl] "}" ;
NoWhere.  ExpWhere ::= Exp ;

Let.        Exp  ::= "let" "{" [Decl] "}" "in" Exp ;
Lam.        Exp  ::= "\\" [PTele] "->" Exp ;
Path.       Exp  ::= "<" [AIdent] ">" Exp ;
Fun.        Exp1 ::= Exp2 "->" Exp1 ;
Pi.         Exp1 ::= [PTele] "->" Exp1 ;
Sigma.      Exp1 ::= [PTele] "*" Exp1 ;
App.        Exp2 ::= Exp2 Exp3 ;
Var.        Exp3 ::= AIdent ;
AppFormula. Exp3 ::= Exp3 "@" Formula ;
U.          Exp3 ::= "U" ;
Fst.        Exp3 ::= Exp3 ".1" ;
Snd.        Exp3 ::= Exp3 ".2" ;
IdP.        Exp3 ::= "IdP" ;
Trans.      Exp3 ::= "transport" ;
Comp.       Exp3 ::= "comp" ;
Glue.       Exp3 ::= "glue" ;
GlueElem.   Exp3 ::= "glueElem" ;
System.     Exp3 ::= "[" [Side] "]" ;
Pair.       Exp3 ::= "(" Exp "," [Exp] ")" ;
coercions Exp 3 ;
separator nonempty Exp "," ;

Dir0.       Dir ::= "0" ;
Dir1.       Dir ::= "1" ;

Face.     Face ::= "(" AIdent "=" Dir ")" ;
separator Face "" ;

Side.   Side ::= [Face] "->" Exp ;
separator Side "," ;

Disj.     Formula  ::= Formula "\\/" Formula1 ;
Conj.     Formula1 ::= Formula1 "/\\ " Formula2 ; -- Why do we need a space?
Neg.      Formula2 ::= "-" Formula2 ;
Atom.     Formula2 ::= AIdent ;
Dir.      Formula2 ::= Dir ;
coercions Formula 2 ;

-- Branches
Branch.   Branch ::= AIdent [AIdent] "->" ExpWhere ;
separator Branch ";" ;

-- Labelled sum alternatives
Label.    Label   ::= AIdent [Tele] ;
separator Label "|" ;

-- Telescopes
Tele.     Tele ::= "(" AIdent [AIdent] ":" Exp ")" ;
terminator Tele "" ;

-- Nonempty telescopes with Exp:s, this is hack to avoid ambiguities
-- in the grammar when parsing Pi
PTele.    PTele ::= "(" Exp ":" Exp ")" ;
terminator nonempty PTele "" ;

position token AIdent ((letter|'_')(letter|digit|'\''|'_')*) ;
separator AIdent "" ;